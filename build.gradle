plugins {
    id 'base'
    id 'org.springframework.boot' version '3.5.9' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.devoceanblue'
version = '0.0.1-SNAPSHOT'
description = 'bsl-backend'

ext {
    set('springModulithVersion', "1.4.6")
}

allprojects {
    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
    }
}

def javaServices = [
    project(':services:search-service'),
    project(':services:autocomplete-service'),
    project(':services:ranking-service'),
]

def pythonServices = [
    project(':services:query-service'),
    project(':services:model-inference-service'),
]

configure(javaServices) {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

configure(pythonServices) {
    apply plugin: 'base'
}
